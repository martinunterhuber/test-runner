plugins {
    id "java"
}

group "org.example"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

sourceSets {
    analyze {
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }

    execute {
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }

    test {
        def dir = "testProject/build/classes/java"
        runtimeClasspath += files(dir + "/main") + files(dir) + files(dir + "/test")
    }
}

configurations {
    analyzeImplementation.extendsFrom implementation
    executeImplementation.extendsFrom implementation
    compileOnly.extendsFrom analyzeImplementation
    compileOnly.extendsFrom executeImplementation
    testImplementation.extendsFrom compileOnly
    testRuntimeOnly.extendsFrom executeRuntimeOnly
}

dependencies {
    def junitVersion = "5.8.2"
    def junitPlatformVersion = "1.8.2"
    def slf4jVersion = "1.8.0-beta4"
    def ckVersion = "0.7.0"
    def pmdVersion = "6.42.0"
    def jGraphVersion = "1.5.1"
    def spotbugsVersion = "4.5.3"

    executeImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    executeRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    executeImplementation "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    executeRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitVersion"
    executeImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
//    executeImplementation "org.slf4j:slf4j-api:$slf4jVersion"
//    executeImplementation "org.slf4j:slf4j-nop:$slf4jVersion"

    analyzeImplementation "org.slf4j:slf4j-api:$slf4jVersion"
    analyzeImplementation "org.slf4j:slf4j-nop:$slf4jVersion"
    analyzeImplementation "com.github.mauricioaniche:ck:$ckVersion"
    analyzeImplementation "net.sourceforge.pmd:pmd-java:$pmdVersion"
    analyzeImplementation "org.jgrapht:jgrapht-core:$jGraphVersion"
    analyzeImplementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    analyzeImplementation "com.github.spotbugs:spotbugs:$spotbugsVersion"
}

test {
    dependsOn(":testProject:assemble")
    dependsOn(":testProject:compileTestJava")
    useJUnitPlatform()
}

class TestTask extends DefaultTask {
    @Input
    @Option(option = "directory", description = "The root directory of the project")
    String directory

    @Input
    @Option(option = "packageName", description = "The name of the package to test")
    String packageName

    @Input
    @Option(option = "commit", description = "The commit to which the current commit will be compared in order to only include the changed(+dependant) classes/tests")
    String commit = ""
}


task doCustomTest(type: TestTask) {
    doFirst {
        def first = true
        fileTree(dir: "$directory").filter { file -> file.toString().endsWith("pom.xml") || file.toString().endsWith(".gradle") }.each { file ->
            def dir = file.getParent()
            if (first && file.toString().endsWith(".gradle")) {
                exec {
                    errorOutput = OutputStream.nullOutputStream()
                    String selfDirectory = workingDir
                    workingDir "$directory"
                    commandLine "./gradlew", "-I", "$selfDirectory/init.gradle", "-q", "copyRuntimeLibs"
                }
            }
            first = false
            javaexec {
                classpath = sourceSets.analyze.runtimeClasspath
                mainClass = "at.unterhuber.test_runner.ProjectAnalyzer"
                String selfDirectory = workingDir
                workingDir = dir
                args = [dir, packageName, selfDirectory, commit, directory]
            }

            javaexec {
                classpath = sourceSets.execute.runtimeClasspath
                classpath += files(dir + "/target/classes") +
                        files(dir + "/target") +
                        files(dir + "/target/test-classes") +
                        fileTree(dir: dir + "/target/dependency") +
                        files(dir + "/build/classes/java/main") +
                        files(dir + "/build/classes/java") +
                        files(dir + "/build/classes/java/test") +
                        files(dir + "/build/resources/test") +
                        fileTree(dir: dir + "/build/libs")
                mainClass = "at.unterhuber.test_runner.TestExecutor"
                workingDir = dir
            }
        }
    }
}

doCustomTest.configure {
    dependsOn assemble
    dependsOn compileTestJava
}